<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312" />
<title>柳永法 ASP生成工具</title>
</head>

<body>
<div>
模板：<input type="text" id="TemplateName" value="Template.asp" />
<input type="checkbox" id="MadeDB" checked /><label for="MadeDB">生成数据库</label>
<input type="checkbox" id="InsertDB" checked /><label for="InsertDB">插入数据</label>
插入多少条：<input type="text" id="InsertDBNumber" value="100" />
<br /><br /><br />
表名：<input type="text" id="TableName" value="NewTableName" />


</div>
<div>字段：
  <textarea name="textarea" id="TableFields" cols="50" rows="10" ></textarea> 
  中文字段名|英文字段名（一行一行）
</div>
<div>
  <input type="submit" name="Submit" value="提交" onclick="Start()" />
</div>

<!--初始化开始-->
<pre id="sDemo" style="display:none" intro="初始源测试数据">
用户名|UserName
密码|Password
性别|Sex
真实姓名|TrueName
年龄|Age
电话|Tel
MSN|MSN
QQ|QQ
Email|Email
</pre>
<!--初始化结束-->

<!--脚本开始-->

<script language="VBScript" type="text/vbscript">
'self.Focus()
'self.ResizeTo 620, 520

'初始化数据
TableFields.Value = sDemo.innerHTML


Function Start()
	CreateWeb ""
	If MadeDB.checked Then CreateDataBase ""
	MsgBox "OK"
End Function

Function CreateDataBase(DBName)
	'建数据库
	dbpath = DBName
	if DBName="" then dbpath=TableName.value & ".mdb"
	Set DB = CreateObject("ADOX.Catalog")
	DB.Create("Provider=Microsoft.Jet.OLEDB.4.0;Data Source="&dbpath)


	'****************************ACCESS**********************************
	dbpath=dbpath
	connstr= "provider=microsoft.jet.oledb.4.0;data source=" & dbpath 
	set conn=createobject("adodb.connection")  
	conn.open connstr
	'*********************************************************************

	'****************************Make ACCESS SQL*****************************************
	insertsql1=""
	insertsql2=""

	sql="CREATE TABLE " & TableName.value  & " ("
	sql=sql & vbCrLf & "	[ID]  AutoIncrement   NOT NULL ,"

	Fields=Split(TableFields.value,vbCrLf)
	for Each Field in Fields
		if len(Field)>=3 then
		Fs=Split(Field,"|")
			sql=sql & vbCrLf & "	[" & Fs(1) & "]  Text (255) NULL  ,"
			insertsql1=insertsql1  & "[" & Fs(1) & "],"
			insertsql2=insertsql2  & "'" & Fs(0) & "',"
		end if
	next

	sql=sql & vbCrLf & "	[Hits]  Integer  NULL  ,"
	sql=sql & vbCrLf & "	[ADDTime]  Date  NULL  ,"
	sql=sql & vbCrLf & "	[ModiTime]  Date  NULL  ,"
	sql=sql & vbCrLf & "	[OrderID]  Integer  NULL  ,"
	sql=sql & vbCrLf & "	[Flags]  Integer  NULL  ,"
	sql=sql & vbCrLf & "	[Remark]  Memo  NULL  ,"
	sql=sql & vbCrLf & "	PRIMARY KEY (ID)"
	sql=sql & vbCrLf & ")"
	'****************************Make ACCESS SQL*****************************************

	'****************************Make MS SQL*****************************************
	mssql="CREATE TABLE " & TableName.value  & " ("
	mssql=mssql & vbCrLf & "	[ID] int primary key identity(1,1),--ID，主键，自动号"

	Fields=Split(TableFields.value,vbCrLf)
	for Each Field in Fields
		if len(Field)>=3 then
		Fs=Split(Field,"|")
			mssql=mssql & vbCrLf & "	[" & Fs(1) & "] varchar(200), --" & Fs(0)
		end if
	next

	mssql=mssql & vbCrLf & "	[Hits] int Default (0),--点击数"
	mssql=mssql & vbCrLf & "	[ADDTime] datetime Default (getdate()),--提交时间"
	mssql=mssql & vbCrLf & "	[ModiTime] datetime Default (getdate()),--提交时间"
	mssql=mssql & vbCrLf & "	[OrderID] int Default (0),--排序号"
	mssql=mssql & vbCrLf & "	[Flags] int Default (0) ,--标识"
	mssql=mssql & vbCrLf & "	[Remark]  text ,"
	mssql=mssql & vbCrLf & ")"
	'****************************Make MS SQL*****************************************

	WriteToFile TableName.value & ".sql", "--ACCESS生成表的SQL语句" & vbCrLf & sql & string(5,vbCrLf) & "--MSSQL生成表的SQL语句" & vbCrLf & mssql,"gb2312"

	conn.execute(sql)

if InsertDB.checked then
	for i=1 to InsertDBNumber.value
		instrtsql="insert into " & TableName.value  & "(" & insertsql1 & "ADDTime,ModiTime,Flags,Hits) values (" & replace(insertsql2,"','",i & "','") & "#" & now() & "#" & ",#" & now() & "#,0," & int(i*1.234) & ")"
		conn.execute(instrtsql)
	next
end if

End Function


Function CreateWeb(filename)
Admin_ADDHTML=""
Admin_EditHTML=""
Admin_ShowHTML=""
Admin_ListHTML1=""
Admin_ListHTML2=""
Admin_SearchHTML=""
Admin_SearchHTML_ASP=""

InDB_VBS=""

WEB_SearchListShow=""

Fields=Split(TableFields.value,vbCrLf)
for Each Field in Fields
	if len(Field)>=3 then
	Fs=Split(Field,"|")
	Admin_ADDHTML = Admin_ADDHTML & vbcrlf & "<tr><td>" & Fs(0) & "：</td><td><input type=""textbox"" name=""" & Fs(1) & """ value="""" /></td></tr>"
	Admin_EditHTML = Admin_EditHTML & vbcrlf & "<tr><td>" & Fs(0) & "：</td><td><input type=""textbox"" name=""" & Fs(1) & """ value=""<%=rs(""" & Fs(1) & """)%>"" /></td></tr>"
	Admin_ShowHTML = Admin_ShowHTML & vbcrlf & "<tr><td>" & Fs(0) & "：</td><td><%=rs(""" & Fs(1) & """)%>　</td></tr>"
	Admin_ListHTML1 =  Admin_ListHTML1 & vbcrlf & "<th>" & Fs(0) & "</th>"
	Admin_ListHTML2 = Admin_ListHTML2 & vbcrlf & "<td><%=rs(""" & Fs(1) & """)%></td>"
	Admin_SearchHTML = Admin_SearchHTML & vbcrlf & Fs(0) & "：<input type=""textbox"" name=""" & Fs(1) & """ value=""<%=" & Fs(1) & "%>"" size=""16"" />"
	Admin_SearchHTML_ASP = Admin_SearchHTML_ASP & Fs(1) & "|"
	InDB_VBS = InDB_VBS & vbcrlf & "rs(""" & Fs(1) & """)	=	Trim(Request(""" & Fs(1) & """))	'" & Fs(0)

	WEB_SearchListShow = WEB_SearchListShow & vbcrlf & "<tr><td>" & Fs(0) & "：</td><td><%=rs(""" & Fs(1) & """)%>"")</td></tr>"

	end if
next

'===================================================================================================
Admin_SearchHTML_ASP=left(Admin_SearchHTML_ASP,len(Admin_SearchHTML_ASP)-1)
Admin_SearchHTML_ASPs=Split(Admin_SearchHTML_ASP,"|")
Admin_SearchHTML_ASP=""
RequestTemplate = ""
RequestTemplate = RequestTemplate & vbCrLf & "$FieldName$ = Trim(Request(""$FieldName$""))"
RequestTemplate = RequestTemplate & vbCrLf & "If $FieldName$<>"""" Then"
RequestTemplate = RequestTemplate & vbCrLf & "    sql = sql & "" and $FieldName$ like '%"" & $FieldName$ & ""%'"""
RequestTemplate = RequestTemplate & vbCrLf & "    'sql = sql & "" and $FieldName$ ='"" & $FieldName$ & ""'"""
RequestTemplate = RequestTemplate & vbCrLf & "    SearchURL = SearchURL & ""&$FieldName$="" & $FieldName$"
RequestTemplate = RequestTemplate & vbCrLf & "End If"

Admin_SearchHTML_ASP=""

for Each Admin_SearchHTML_ASP_Temp in Admin_SearchHTML_ASPs
	Admin_SearchHTML_ASP = Admin_SearchHTML_ASP & vbCrLf & replace(RequestTemplate,"$FieldName$",Admin_SearchHTML_ASP_Temp)
next

'===================================================================================================


template=ReadFromFile(TemplateName.value, "gb2312")
template=replace(template,"$TableName$",TableName.value)
template=replace(template,"$Admin_ADDHTML$",Admin_ADDHTML)
template=replace(template,"$Admin_EditHTML$",Admin_EditHTML)
template=replace(template,"$Admin_ShowHTML$",Admin_ShowHTML)
template=replace(template,"$Admin_ListHTML1$",Admin_ListHTML1)
template=replace(template,"$Admin_ListHTML2$",Admin_ListHTML2)
template=replace(template,"$Admin_SearchHTML$",Admin_SearchHTML)
template=replace(template,"$Admin_SearchHTML_ASP$",Admin_SearchHTML_ASP)

template=replace(template,"$InDB_VBS$",InDB_VBS)
template=replace(template,"$WEB_SearchListShow$",WEB_SearchListShow)

WriteToFile TableName.value & ".asp", template,"gb2312"

WriteToFile "dbpath.asp", "<%dbpath=""" & TableName.value & ".mdb""%>","gb2312"

End Function

'<---------------------------------------Adodb.Stream操作--------------------------------------->
'按指定编码读取文件
Function ReadFromFile(FileUrl, CharSet)
    Dim Str
    Set stm = CreateObject("Adodb.Stream")
    stm.Type = 2
    stm.mode = 3
    stm.charset = CharSet
    stm.Open
    stm.loadfromfile FileUrl
    Str = stm.readtext
    stm.Close
    Set stm = Nothing
    ReadFromFile = Str
End Function

'按指定编码存储文件
Function WriteToFile (FileUrl, Str, CharSet)
    Set stm = CreateObject("Adodb.Stream")
    stm.Type = 2
    stm.mode = 3
    stm.charset = CharSet
    stm.Open
    stm.WriteText Str
    stm.SaveToFile FileUrl, 2
    stm.flush
    stm.Close
    Set stm = Nothing
End Function
'<---------------------------------------/ Adodb.Stream操作------------------------------------->



</script>
<!--脚本结束-->
</body>
</html>
